apply plugin: 'maven'
apply plugin: 'signing'

dependencies {

    compile group: 'com.github.tomakehurst', name: 'wiremock-standalone', version: '2.8.0'
    compile 'org.springframework:spring-test'
    compile 'org.springframework:spring-web'
    compile 'org.hamcrest:hamcrest-library'
    compile group: 'junit', name: 'junit'
    compile group: 'org.skyscreamer', name: 'jsonassert'

    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'
    testCompile group: 'org.codehaus.groovy', name: 'groovy-all'
    testCompile 'org.springframework.boot:spring-boot-starter-web'
}

jar {
    archiveName "${rootProject.name}-${rootProject.version}.jar"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveName "${rootProject.name}-${rootProject.version}-javadoc.jar"
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    archiveName "${rootProject.name}-${rootProject.version}-sources.jar"
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

def publishStage() {
    project.hasProperty('publishStage')? publishStage: false
}

if (publishStage()) {

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    groupId rootProject.group
                    artifactId rootProject.name
                    version rootProject.version

                    name rootProject.name
                    packaging 'jar'
                    description 'Shared contract driven development with WireMock and Spring MockMvc'
                    url 'https://github.com/Hippoom/wiremock-contract-verifier'

                    scm {
                        url 'scm:https://github.com/Hippoom/wiremock-contract-verifier'
                        connection 'scm:https://github.com/Hippoom/wiremock-contract-verifier'
                        developerConnection 'scm:https://github.com/Hippoom/wiremock-contract-verifier'
                    }

                    licenses {
                        license {
                            name 'Apache License'
                            url 'https://github.com/Hippoom/wiremock-contract-verifier/blob/master/LICENSE'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'hippoom'
                            name 'Yugang Zhou'
                        }
                    }
                }
            }
        }
    }
}
