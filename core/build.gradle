plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'maven-publish'

dependencies {

    compile group: 'com.github.tomakehurst', name: 'wiremock-standalone', version: '2.8.0'
    compile 'org.springframework:spring-test'
    compile 'org.springframework:spring-web'
    compile 'org.hamcrest:hamcrest-library'
    compile group: 'junit', name: 'junit'
    compile group: 'org.skyscreamer', name: 'jsonassert'

    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'
    testCompile group: 'org.codehaus.groovy', name: 'groovy-all'
    testCompile 'org.springframework.boot:spring-boot-starter-web'
}

jar {
    archiveName "${rootProject.name}-${rootProject.version}.jar"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveName "${rootProject.name}-${rootProject.version}-javadoc.jar"
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    archiveName "${rootProject.name}-${rootProject.version}-sources.jar"
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = rootProject.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Hippoom/wiremock-contract-verifier.git'
        version {
            gpg {
                sign = true
                passphrase = System.getenv('GPG_PASSPHRASE')
            }
        }
    }
    publications = ['MyPublication']
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "https://github.com/Hippoom/wiremock-contract-verifier/blob/master/LICENSE"
            distribution "repo"
        }
    }

    developers {
        developer {
            id 'hippoom'
            name 'Yugang Zhou'
        }
    }

    scm {
        url 'scm:https://github.com/Hippoom/wiremock-contract-verifier'
        connection 'scm:https://github.com/Hippoom/wiremock-contract-verifier'
        developerConnection 'scm:https://github.com/Hippoom/wiremock-contract-verifier'
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            groupId rootProject.group
            artifactId rootProject.name
            version rootProject.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Your description of the lib')
                root.appendNode('name', 'Your name of the lib')
                root.appendNode('url', 'https://site_for_lib.tld')
                root.children().last() + pomConfig
            }
        }
    }
}


