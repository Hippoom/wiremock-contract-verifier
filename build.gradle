plugins {
    id "io.spring.dependency-management" version "1.0.3.RELEASE" apply false
}

allprojects {
    apply plugin: 'idea'
    group = 'com.github.hippoom'
    version = '0.2.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'

    apply from: "${rootProject.projectDir}/gradle/checkstyle.gradle"
    apply from: "${rootProject.projectDir}/gradle/findbugs.gradle"
    apply from: "${rootProject.projectDir}/gradle/git-hooks.gradle"


    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) { options.encoding = 'utf-8' }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "io.spring.platform:platform-bom:${springPlatformVersion}"
        }

        dependencies {
            dependency "org.projectlombok:lombok:${lombokVersion}"
            dependency "com.google.code.findbugs:annotations:${findbugsVersion}"
            dependency group: 'org.modelmapper', name: 'modelmapper', version: '1.1.0'
            dependency group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.9'
            dependency("org.hamcrest:hamcrest-library:1.3")
            dependency group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'
            dependency group: 'junit', name: 'junit', version: '4.12'
        }
    }

    dependencies {
        compile("org.slf4j:slf4j-api")
        testCompile("org.hamcrest:hamcrest-library:1.3")

        compileOnly "org.projectlombok:lombok"
        testCompileOnly "org.projectlombok:lombok"
        compileOnly 'com.google.code.findbugs:annotations'
        testCompileOnly 'com.google.code.findbugs:annotations'
    }

    idea {
        module {
            scopes.COMPILE.plus += [configurations.compileOnly]
        }
    }

    test.finalizedBy jacocoTestReport
}


idea {
    project {

        jdkName = '1.8'
        languageLevel = '1.8'

        ipr.withXml { xmlFile ->
            // setup Git root
            xmlFile.asNode().component.find { it.@name == 'VcsDirectoryMappings' }.replaceNode {
                component(name: 'VcsDirectoryMappings') {
                    mapping(directory: "", vcs: "Git")
                    mapping(directory: "\$PROJECT_DIR\$", vcs: 'Git')
                }
            }

            // setup annotationProcessing for lombok
            xmlFile.asNode().component.find {
                it.@name == 'CompilerConfiguration'
            }.annotationProcessing.replaceNode {
                annotationProcessing() {
                    profile(default: "true", name: "Default", enabled: 'true')
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}
